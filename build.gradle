

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath("com.ofg:uptodate-gradle-plugin:0.0.3")
	}
}

apply plugin: 'groovy'
apply plugin: 'scala'
apply plugin: 'maven'

Boolean doSigning() {
	signingEnabled.trim() == "true"
}

String createVersion(String base, Boolean isSnapshot) {
	base + (isSnapshot ? "-SNAPSHOT" : "")
}

ext {
    gatlingVersion = "1.5.6"
	baseVersion = "0.1"
	isSnapshot = true

	projectArtifactName = "gradle-gatling-plugin"
	projectTitle = "Gatling Gradle Plugin"
	projectDescription = "A Gatling plugin for Gradle"
	projectUrl = "https://github.com/mperry/gradle-gatling-plugin"
	scmUrl = "https://github.com/mperry/gradle-gatling-plugin.git"

	founder = "Mark Perry"
	founderUsername = "mperry"

	sonatypeBaseUrl = "https://oss.sonatype.org"
	sonatypeSnapshotUrl = "$sonatypeBaseUrl/content/repositories/snapshots/"
	sonatypeRepositoryUrl = "$sonatypeBaseUrl/content/groups/public"
	sonatypeReleaseUrl = "$sonatypeBaseUrl/service/local/staging/deploy/maven2/"
	sonatypeUploadUrl = isSnapshot ? sonatypeSnapshotUrl : sonatypeReleaseUrl

}

defaultTasks "build"

version = createVersion(baseVersion, isSnapshot)
group = "com.github.mperry"
archivesBaseName = projectArtifactName

repositories{
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public" }
    maven { url 'http://repository.excilys.com/content/groups/public' }
}

dependencies {
    compile gradleApi()
    compile "org.functionaljava:functionaljava:4.2-beta-1"
    compile("com.github.mperry:functionalgroovy-main:0.6") {
        exclude module: "groovy-all"
    }
    compile "com.excilys.ebi.gatling:gatling-app:$gatlingVersion"
    compile "com.excilys.ebi.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

uploadArchives {
	enabled = true
	repositories {
		mavenDeployer {
			if (doSigning()) {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			}

			repository(url: sonatypeUploadUrl) {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			pom {
				groupId = project.group
				project {
					name projectTitle
					packaging 'jar'
					description projectDescription
					url projectUrl
					organization {
						name projectTitle
						url projectUrl
					}
					scm {
						url scmUrl
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id project.founderUsername
							name project.founder
						}

					}
				}
			}
		}
	}

}

